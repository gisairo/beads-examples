//  a patek philippe wristwatch simulation
#beads level 1 module ClockPatek title "Wristwatch Simulation"

//  art resources that we will use later
resource local:"/code/ClockPatek/art/" remote:"http://magicmouse.com/beads/examples/watch/"
	file:"patek_cleaned_750v.jpg" label:watch_background 
	file:"hand_day.png" label:day_hand 
	file:"hand_min.png" label:minute_hand 
	file:"hand_hour.png" label:hour_hand 
	file:"hand_sec.png" label:second_hand  

var g : a_date //  our global state variable, a record containing fields date_minute, etc.

const
	DEG_PER_DAY  = 360/31
	DEG_PER_SEC  = 360/60  // also used for hour
	DEG_PER_HOUR = 360/12

	CENTER_X = 485  // point that hour/min/sec hands rotate around
	CENTER_Y = 380

-----------------
calc main_init `main init procedure`
-----------------
	//runtime.time_scale = 7200  // speed up time so that each second we cover 2 hours
	//  set up a callback to 'do_tick' every second, starting immediately
	//  note that if we didn't start immediately, then our 'g' value would have no value on first draw
	loom_callback (do_tick, delay:0, interval:1, reps:10000)

-----------------
calc do_tick `is called once a second`
-----------------
	seconds_to_date() ===> g  // merge the current date/time into g
	
----------------
draw main_draw `main drawing procedure` 
----------------
	draw_image (watch_background) // draw in upper left corner

	var angle = g.date_day*DEG_PER_DAY - 90  // day angle
	draw_image (day_hand, x:486, y:512, originx:11, originy:11, angle:angle deg)

	angle = (g.date_hour + g.date_minute/60)*DEG_PER_HOUR - 90  //  hour angle, moves smoothly
	draw_image (hour_hand, x:CENTER_X, y:CENTER_Y, originx:11, originy:18, angle:angle deg)
	
	angle = g.date_minute*DEG_PER_SEC - 90  //  minute angle, jumps to each minute
	draw_image (minute_hand, x:CENTER_X, y:CENTER_Y, originx:11, originy:12, angle:angle deg)

	angle = g.date_second*DEG_PER_SEC - 90  // sec angle, jumps to each second
	draw_image (second_hand, x:CENTER_X, y:CENTER_Y, originx:18, originy:17, angle:angle deg)
